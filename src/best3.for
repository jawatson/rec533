c###best3.for
      SUBROUTINE BEST3
C ----------------------------------------------------------------------
C       SUBROUTINE BEST3:
C       SORTS FREQUENCIES, ACCORDING TO 'CLOSEST' TO THE OPTINUM
C       WORKING FREQUENCIES OVER A SPECIFIED UT INTERVAL, FOR
C       GRAPHICAL PRESENTATION (OUTPUT TYPE 5).
C       THE FREQUENCIES ARE SELECTED FROM THE INPUT SET (MAX.=11)
C       AND ARE THOSE WITH THE LARGEST TOTAL WEIGHTS.
C       FOR A GIVEN FREQUENCY F AND TIME, WEIGHT=1.0 AT F=FOT
C       DECREASING TO ZERO AT F<=LUF AND F>=OPERATIOAL MUF
C ----------------------------------------------------------------------
      INTEGER KA(7),KB(7)
      REAL LUFT
      COMMON /FRQ/ FREL(12),FREL5(11,7),FW5(11,7),MAXF,MF,KOF,FREQ
     A, JKF,FXEMAX
      character*4 IRLAT,IRLONG,ITLAT,ITLONG,IRCVR,ITRANS
      COMMON/ION/ IRLAT,IRLONG,ITLAT,ITLONG,IRCVR(5),ITRANS(5),IT,ITRUN
     A,IT1,IT2,ITSTEP,KM,IA,METHOD,ICON,NPSL,NYEAR,NPAGO,ICGM
      COMMON/MUFS/ EMUF(24,3),F2MUF(24,6),ALLMUF(24),EFMUF(24),FOT(24)
     A ,FOT1(24),XLUF(24),ZLUF(24),OPMUF(24),OPMUF1(24),FU(24),FL(24)
     B, KFN,LUFC,NLOOP,ACT1,DXL,XMODE,FX,PR
C ----------------------------------------------------------------------
      DATA KA/2,1,5,9,13,17,21/,KB/25,5,9,13,17,21,25/
C.....EACH FREQUENCY
      DO 3 JF=1,MAXF
C.....STORE FREQUENCIES IN ARRAY FREL5
      F=FREL(JF)
      IF(F.LE.0.01) GO TO 4
      MFX=JF
C......EACH UT TIME
      DO 1 K=1,7
      KT1=KA(K)
      KT2=KB(K)
      SW=0.0
      DO 5 KT=KT1,KT2
      IT=KT-1
      IF(IT.EQ.0) IT=24
      LUFT=XLUF(IT)
      FOTT=FOT(IT)
      OPMUFT=OPMUF(IT)
C......CALCULATE 'CLOSENESS' WEIGHT FOR A GIVEN TIME AND FREQUENCY
      W=WEIGHT(F,LUFT,FOTT,OPMUFT)
      SW=SW+W
    5 CONTINUE
C......CALCULATE SUM OF WEIGHTS FOR EACH FREQUENCY
    2 FREL5(JF,K)=F
      FW5(JF,K)=SW
    1 CONTINUE
    4 CONTINUE
    3 CONTINUE
C.....SORT FREQUENCIES,WEIGHTING FACTORS INTO DECENDING ORDER
      MF1=MFX-1
      DO 15 K=1,7
      DO 15 M=1,MF1
      MF2=MFX-M
      DO 11 L=1,MF2
      L1=L+1
C......SORT ACCORDING TO WEIGHTING FACTORS
      IF(FW5(L,K).GT.FW5(L1,K)) GO TO 11
C.....INTERCHANGE ELEMENTS
      TEMPF=FREL5(L,K)
      FREL5(L,K)=FREL5(L1,K)
      FREL5(L1,K)=TEMPF
      TEMPW=FW5(L,K)
      FW5(L,K)=FW5(L1,K)
      FW5(L1,K)=TEMPW
   11 CONTINUE
   15 CONTINUE
C......FREQUENCIES WITH THE LARGEST WEIGHTING FACTORS ARE
C......IN DESENDING ORDER FREL5(1),FREL5(2).....
C ----------------------------------------------------------------------
      RETURN
      END
