      SUBROUTINE GEOM
C.....
C.....VERSION 11.JAN.1995 (re-written using BLOCK IF statements)
C.....THIS ROUTINE CALCULATES THE PATH GEOMETRY
C.....MAPIN IS -1,IF DATA/DISK BASE HAS NOT BEEN READ.
C.....
      LOGICAL LHOP
      REAL*4 NP0,NP1,NP2,NP3,NP4,NP5,NP6
      CHARACTER*40 VERSN
      COMMON /CON/D2R, DCL, GAMA, PI, PI2, PIO2, R2D, RO, VOFL
      COMMON /DON/AMIN,AMIND,BTR,BTRD,BRTD,GCD,GCDKM,PWR,XPW,APW
     A ,RLAT,RLATD,RLONG,RLONGD,SSN,TLAT,TLATD,TLONG,TLONGD,PLAT,PLONG
     B ,PGLAT,ACAV,ASM,FEAV,GMT,VERSN,XLZ,GCDFTZ,GCDEND,XINTS,XINTL
     C ,FLUX,CY12(5),DMXA(24),DMX0,CYRAD(5)
      COMMON/GEOG/ABIY(5),CLAT(5),CLONG(5),CLCK(5),EPSPAT(5),F2M3(5)
c>>(b)WP-6A SEPT. 93 MODS add 2 lines REMOVE 1 LINE
     A,FI(3,5),GLAT(5),GMDIP(5),GYZ100(5),GYR100,GYZ300(5),HPF2(5),RD(5)
     B,SIGPAT(5),CYCEN(5)
CX   A,FI(3,5),GLAT(5),GMDIP(5),GYZ(5),HPF2(5),RD(5),SIGPAT(5),CYCEN(5)
c>>(b)WP-6A SEPT. 93 MODS end of change
      character*4 IRLAT,IRLONG,ITLAT,ITLONG,IRCVR,ITRANS
      COMMON /ION/ IRLAT,IRLONG,ITLAT,ITLONG,IRCVR(5),ITRANS(5),IT,ITRUN
     A,IT1,IT2,ITSTEP,KM,IA,METHOD,ICON,NPSL,NYEAR,NPAGO,ICGM
      COMMON /D2HOP/ LHOP,NK,KM1,KM2,KM3
C.WP6A-DG5  COMMON USED IN GEOM, LUFFY AND ABSORP
      COMMON /ABSOR/ GYL(5),TAF(5),PEXP(5),ASSN                   ! C.WP6A-DG5
      DATA EPSLON/1.0E-6/
      DATA GLT,GLG/1.37,1.19/
      DATA NP0,NP1,NP2,NP3,NP4,NP5,NP6
     1/60.900,28.119,-42.933,-52.064,108.799,23.945,-61.867/
      DATA SP0,SP1,SP2,SP3,SP4,SP5,SP6
     1/-69.700,41.568,37.500,-66.509, -4.266,24.940,-22.933/
C-----SIXTH ORDER CHEBYSHEV POLYNOMIAL
C.....COEFFICIENTS HAVE BEEN SELECTED TO THE GIVE LOCUS OF GEOGRAPHIC
C.....LATITUDE (FOR AN INPUT LONGITUDE) CORRESPONDING TO 60 N AND
C.....60 S CORRECTED GEOMAGNETIC REPECTIVELY. THENCE, IT CAN BE
C.....CONFIRMED IF A GIVEN LOCATION IS ABOVE 60 DEG. C.G. LAT.(N OR S)
      CHEB6(X,P0,P1,P2,P3,P4,P5,P6)
     1 =P0+X*(P1+X*(P2+X*(P3+X*(P4+X*(P5+X*P6)))))
C-----GEOMAGNETIC NORTH POLE, LATITUDE, LONGITUDE, MINIMUM
C.....TAKE OFF ANGLE SERVICE PROBABILITY FACTOR
C.....LATITUDE SOUTH IS "-" AND LATITUDE NORTH IS "+"
C.....LONGITUDE EAST IS "-" AND LONGITUDE WEST IS "+"
C.....IF LHOP .TRUE. RE-EVALUATE REFLECTION AREA COORDINATES RD(2),RD(4)
      IF(LHOP) GO TO 66
      NK=1
      KM1=1
      KM3=1
      ICGM=0
      TLAT=TLATD * D2R
      TLONG=-TLONGD * D2R            !  can you believe EAST is negative????
      RLAT=RLATD * D2R
      RLONG=-RLONGD * D2R            !  WOW!!!
      if(abs(RLATD).ge.89.9) RLONG=0.        !  at poles, force long=0
      if(abs(TLATD).ge.89.9) TLONG=0.        !  at poles, force long=0
      if(RLATD.ge.89.9) RLAT=89.9*D2R        !  move away from North pole
      if(RLATD.le.-89.9) RLAT=-89.9*D2R      !  move away from South pole
C.... GREAT CIRCLE DISTANCE AND BEARINGS.
      DLONG=TLONG-RLONG
      IF(ABS (DLONG) .GT. PI) DLONG=DLONG-SIGN (PI2, DLONG)
      QCOS=SIN(TLAT)*SIN(RLAT)+COS(TLAT)*COS(RLAT)*COS(DLONG)
      IF(ABS (QCOS) .GT. 1.0) QCOS=SIGN (1.0, QCOS)
      GCD=ACOS (QCOS)
C.....MINIMUM DISTANCE IS 31.85 METERS
      IF(GCD.LT.0.000001) GCD=0.000001
C.....FOR GREAT CIRCLE PATH
      GCDKM=GCD * RO
C.....
C.....CHECK IF TRANSMITTER IS NEAR A POLE
      IF(COS(TLAT).LE.EPSLON) THEN
C......TRANSMITTER IS NEAR A POLE
       IF(TLAT.LE.0.0) THEN
        BTR= 0.0
       ELSE
        BTR=PI
       END IF
      ELSE
C.....TRANSMITTER IS NOT NEAR A POLE
       QCOS=(SIN(RLAT)-SIN(TLAT)*COS(GCD))/(COS(TLAT)*SIN(GCD))
       IF(ABS (QCOS) .GT. 1.0) QCOS=SIGN (1.0, QCOS)
C......BTR IS BEARING, TRANSMITTER TO RECEIVER
       BTR=ACOS (QCOS)
      END IF
      IF(DLONG .LT. 0.0) BTR=PI2-BTR
C.....
C.....CHECK IF RECEIVER IS NEAR A POLE
      IF(COS(RLAT).LE.EPSLON) THEN
C......RECEIVER IS NEAR A POLE
       IF(RLAT.LE.0.0) THEN
        BRT=0.
       ELSE
        BRT=PI
       END IF
C.....RECEIVER IS NOT NEAR A POLE
      ELSE
       QCOS=(SIN(TLAT)-SIN(RLAT)*COS(GCD))/(COS(RLAT)*SIN(GCD))
       IF(ABS(QCOS).GT.1.0) QCOS=SIGN (1.0, QCOS)
C......BRT IS BEARING, RECEIVER TO TRANSMITTER
       BRT=ACOS (QCOS)
      END IF
C.....
      IF(DLONG .GT. 0.0) BRT=PI2-BRT
      IF(NPSL .NE. 0) THEN
       DLONG=DLONG-SIGN(PI2,DLONG)
       GCD=PI2-GCD
       GCDKM=GCD*RO
       BTR=BTR+PI
       BRT=BRT+PI
       IF(BTR .GT. PI2) BTR=BTR-PI2
       IF(BRT .GT. PI2) BRT=BRT-PI2
      END IF
C.....CONVERT BEARINGS TO DEGREES
      BTRD=BTR * R2D
      BRTD=BRT * R2D
C     DETERMINATION OF REFLECTION AREAS.
C SELECT SAMPLE AREAS IN ORDER
C.....FOR 1`ST ESTIMATE OF CONTROL POINT LOCATIONS FOF F-MODES
C.....ASSUME HOP LENGTH OF DMX0=3400 KM (assumed. min. value for Dmax)
c.....REP VALUE of 4000 km
      DMX0=3400.
C.....
      IF(GCDKM.LE.2000.0) THEN
C.....KM IS THE NUMBER OF SAMPLE AREAS
       KM=1
       MID=1
       RD(1)= GCD/2.
      ELSE IF(GCDKM.GT.2000.0.AND.GCDKM.LE.DMX0) THEN
       KM=3
       MID=2
       RD(1)= 1000. / RO
       RD(2)= GCD/2.
       RD(3)= GCD -RD(1)
      ELSE
       KM= 5
       MID=3
       NK=INT(GCDKM/DMX0)+1
       FDIST= GCDKM/(FLOAT(NK)*2.0*RO)
       RD(1)= 1000.0/RO
       RD(2)= FDIST
       RD(3)= GCD/2.
       RD(4)= GCD-FDIST
       RD(5)= GCD-RD(1)
      END IF
C.....
   66 CONTINUE
C.....RD(1) IS E LAYER, RD(2) IS F LAYER, RD(3) IS ALL LAYERS,
C.....RD(4) IS F LAYER AND RD(5) IS E LAYER
C     REFLECTION AREA COORDINATES AND GEOMAGNETIC LATITUDE.
      IF(KM.LT.1) GO TO 185
c>>(c)WP-6A SEPT. 93 MODS add 2 lines
      GYR100=0.0
      IKM=0
      DO 180 I=KM1,KM,KM3
c>>(c)WP-6A SEPT. 93 MODS add 1 line (for count IKM)
      IKM=IKM+1
      DRF=RD (I)
      CTLAT=COS(TLAT)
C....
C.....CHECK IF TRANSMITTER IS NEAR A POLE
      IF(CTLAT.LT.EPSLON) THEN
C.....TRANSMITTER IS NEAR A POLE
       RFLT=TLAT-SIGN(DRF,TLAT)
       IF(ABS(RFLT).GT.PIO2) RFLT=SIGN(PIO2,RFLT)
       RFLG=RLONG
      ELSE
C.....TRANSMITTER IS NOT NEAR A POLE
       QCOS=COS(DRF)*SIN(TLAT) + SIN(DRF)*COS(TLAT)*COS(BTR)
       IF(ABS (QCOS) .GT. 1.0) QCOS=SIGN (1.0, QCOS)
       RFLT=PIO2-ACOS (QCOS)
C.....CHECK IF THE SAMPLE AREA IS NEAR A POLE
       IF(COS(RFLT).LE.EPSLON) THEN
C......SAMPLE AREA IS NEAR A POLE
        RFLG=TLONG
       ELSE
C......SAMPLE AREA IS NOT NEAR A POLE
        QCOS=(COS(DRF)-SIN(RFLT)*SIN(TLAT))/(COS(RFLT)*COS(TLAT))
        IF(ABS (QCOS) .GT. 1.0) QCOS=SIGN (1.0, QCOS)
        RFLG=ACOS (QCOS)
        IF(DRF .GE. PI) RFLG=PI2-RFLG
        RFLG=TLONG-SIGN (RFLG, DLONG)
        IF(ABS (RFLG) .GT. PI) RFLG=RFLG-SIGN (PI2, RFLG)
       END IF
      END IF
C.....
      QCOS=SIN(GLT)*SIN(RFLT)+COS(GLT)*COS(RFLT)*COS(RFLG-GLG)
      IF(ABS (QCOS) .GT. 1.0) QCOS=SIGN (1.0, QCOS)
C.....GLAT(I) IS THE GEOMAGNETIC LATITUDE
C.....CLAT(I) IS THE GEOGRAPHIC LATITUDE
C.....CLONG(I) IS THE GEOGRAPHIC LONGITUDE
C.WP6A.DG9 ADD/MODIFY 20 LINES TO PROTECT AGAINST ROUNDING ERRORS (ESP. FOR
C.....FOR LOCATIONS NEAR POLES) MID 20 MAY 1993
C.....CHECK THAT -90 DEG < LATITUDE < 90 DEG
      IF(ABS(RFLT).GT.PIO2) RFLT=SIGN(PIO2,RFLT)
C.....CHECK THAT -180 DEG < LONGITUDE < 180 DEG
      IF(ABS(RFLG).GT.PI) RFLG=SIGN(PI,RFLG)
      GAT=ACOS (QCOS)
      GLAT (I)=PIO2-GAT
C.DG9...END
      CLAT (I)=RFLT
      CLONG (I)=RFLG
C.....MAGNETIC FIELD VARIABLES.
      CENLAT=RFLT*R2D
      CENLG=RFLG*R2D
      IF(CENLG .GT. 0.0) CENLG=360.0-CENLG
      IF(CENLG .LT. 0.0) CENLG=ABS(CENLG)
c>>(b)WP-6A SEPT. 93 MODS add 6 lines ; calc. mag. field parameters
C.....Calc. mag. field parameters for heights 100 and 300 km
      HEIGHT=100.
      CALL MAGFIT(CENLAT,CENLG,HEIGHT,DIP100,GYZ100(I))
      GYR100=GYR100+GYZ100(I)
      HEIGHT=300.
      CALL MAGFIT(CENLAT,CENLG,HEIGHT,DIP300,GYZ300(I))
c>>(b)WP-6A SEPT. 93 MODS end of change
      CRFLT=COS(RFLT)
      GMDIP(I)=ATAN(DIP300/SQRT(COS(RFLT)))
C.WP6A-DG5  LONGITUDINAL GYROFREQUENCY, TOTAL ABSORPTION FACTOR AND
C.WP6A-DG5  EXPONENT P FOR ABSORPTION CALCULATION
      GYL(I)=ABS(GYZ100(I)*SIN(DIP100))                           ! C.WP6A-DG5
      CALL CONTAT(GMDIP(I),CENLAT,TAF(I))                         ! C.WP6A-DG5
      CALL CONTP(GMDIP(I),CENLAT,PEXP(I))                         ! C.WP6A-DG5
C.....
C.....FOR PATH LENGTHS WITH CONTROL POINTS 1000 KM FROM TRANSMITTER
C.....AND RECEIVER (I.E. PATHS LENGTHS ABOVE 2000 KM) EXAMINE IF
C.....CONTROL POINTS ABOVE 60 DEG. CORR GEOMAGNETIC LAT (N OR S).IF SO
C.....SET ICGM=1 AND USE APPOPIATE FADING ALLOWENCES(TABLE 3 REP 266)
      IF(KM.GE.3) THEN
        XENLG=CENLG
        IF(XENLG.GT.180.0) THEN
          XENLG=XENLG-360.0
        END IF
        XN=XENLG/180.0
        IF(CENLAT.GE.0.0) THEN
C......N. HEMISPHERE
          XEN60=CHEB6(XN,NP0,NP1,NP2,NP3,NP4,NP5,NP6)
          IF(CENLAT.GE.XEN60) ICGM=1
        ELSE
C......S. HEMISPHERE
          XEN60=CHEB6(XN,SP0,SP1,SP2,SP3,SP4,SP5,SP6)
          IF(CENLAT.LE.XEN60) ICGM=1
        END IF
      END IF
  180 CONTINUE
c>>(c)WP-6A SEPT. 93 MODS add 1 lines
c.....determine mean gyrofreq-frequency, GYR100, at 100 km
      GYR100=GYR100/FLOAT(IKM)
  185 CONTINUE
C.....PLAT(+N),PLONG(+W)=LAT,LONG. OF PATH MID-POINT
      PGLAT=GLAT(MID)
      PLAT=CLAT(MID)
      PLONG=CLONG(MID)
      RETURN
      END
c-------------------------------------------------------------------
